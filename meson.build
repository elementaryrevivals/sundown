# Project name, programming language and version
project (
    'com.github.watsonprojects.sundown',
    'vala', 'c',
    version: '0.0.1'
)

# Translation module
i18n = import ('i18n')

# GNOME module
gnome = import ('gnome')

# Project arguments
add_project_arguments (
    '-DGETTEXT_PACKAGE="@0@"'.format (meson.project_name ()),
    language: 'c'
)

add_project_arguments(
    ['--vapidir', join_paths(meson.current_source_dir(), 'vapi')],
    language: 'vala'
)


# Listing dependencies
dependencies = [
    dependency('gtk+-3.0'),
    dependency('gee-0.8'),
    meson.get_compiler ('c').find_library ('m', required : false)
]

# Compiling resources
asresources = gnome.compile_resources (
    'as-resources',
    'data/' + meson.project_name () + '.gresource.xml',
    source_dir: 'data',
    c_name: 'as'
)

# Executable
executable (
    meson.project_name (),
    asresources,
    'src/Knob.vala',
    'src/Sundown.vala',
    dependencies: dependencies,
    install: true
)

subdir ('data')
subdir ('po')

meson.add_install_script ('meson/post_install.py')
